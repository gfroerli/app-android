apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'project-report'

buildProperties {
    secrets {
        using project.file('secrets.properties')
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "ch.coredump.watertemp"
        applicationIdSuffix ".zh"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 8
        versionName "0.5.0"

        buildConfigString 'GFROERLI_API_KEY_PUBLIC', buildProperties.secrets['gfroerli_api_key_public']
        buildConfigString 'MAPBOX_ACCESS_TOKEN', buildProperties.secrets['mapbox_access_token']

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'disableAnalytics', 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.all {
            // Whether failing tests should fail the build
            ignoreFailures false

            // Logging output
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha02'

    // Mapbox SDK
    implementation('com.mapbox.mapboxsdk:mapbox-android-sdk:6.8.1')

    // REST client
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // Java 8 date/time API backport
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    // Relative time calculation
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'

    // Baseline alignment of typography
    implementation 'com.actinarium.aligned:aligned:0.1'

    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
