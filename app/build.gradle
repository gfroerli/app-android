apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'project-report'

buildProperties {
    secrets {
        using project.file('secrets.properties')
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "ch.coredump.watertemp"
        applicationIdSuffix ".zh"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 10
        versionName "0.6.0"

        buildConfigString 'GFROERLI_API_KEY_PUBLIC', buildProperties.secrets['gfroerli_api_key_public']
        buildConfigString 'MAPBOX_ACCESS_TOKEN', buildProperties.secrets['mapbox_access_token']

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'disableAnalytics', 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.all {
            // Whether failing tests should fail the build
            ignoreFailures false

            // Logging output
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"

    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
    implementation 'com.google.android.material:material:1.4.0-alpha01'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Mapbox SDK
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.2.1' // Note: 9.3+ switches to proprietary components
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.9.0'

    // REST client
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    // Java 8 date/time API backport
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'

    // Relative time calculation
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.0.Final'

    // Baseline alignment of typography
    implementation 'com.actinarium.aligned:aligned:0.1'

    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    // Image loading
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // Testing
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
